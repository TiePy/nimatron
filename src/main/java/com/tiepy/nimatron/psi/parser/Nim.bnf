/*
 * Copyright 2020 TiePy Ltd. All rights reserved.
 * DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
 *
 * This file is part of Nimatron, a Nim plug-in for IDEA platform IDEs.
 *
 * Nimatron is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.  TiePy Ltd designates this
 * particular file as subject to the "Classpath" exception as provided
 * by TiePy Ltd in the LICENSE file that accompanied this code.
 *
 * Nimatron is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Nimatron.  If not, see <https://www.gnu.org/licenses/>.
 *
 * Please contact TiePy Ltd, 19 Lyall Way, Laurencekirk, AB30 1FU, UK
 * or visit www.tiepy.com if you need additional information or have any
 * questions.
 */

{
    parserClass="com.tiepy.nimatron.psi.parser.NimParser"

    implements="com.tiepy.nimatron.psi.NimElement"
    extends="com.tiepy.nimatron.psi.impl.NimElementImpl"

    psiClassPrefix="Nim"
    psiImplClassSuffix="Impl"
    psiPackage="com.tiepy.nimatron.psi"
    psiImplPackage="com.tiepy.nimatron.psi.impl"

    parserUtilClass="com.tiepy.nimatron.manualParsing.NimParserUtil"

    elementTypeHolderClass="com.tiepy.nimatron.psi.NimElementTypes"
    elementTypeClass="com.tiepy.nimatron.psi.NimElementType"
    tokenTypeClass="com.tiepy.nimatron.psi.NimTokenType"

    implements("namedRoutine")="com.tiepy.nimatron.psi.NimNamedElement"
    mixin("namedRoutine")="com.tiepy.nimatron.psi.impl.NimNamedElementImpl"

    tokens = [
        BOOL_LIT="BOOL_LIT"
        BRACKET="BRACKET"
        C_COMMA="C_COMMA"
        C_GRAVE_ACCENT="C_GRAVE_ACCENT"
        C_SEMICOLON="C_SEMICOLON"
        CHAR_LIT="CHAR_LIT"
        COMMENT="COMMENT"
        DEDENT="DEDENT"
        FLOAT32_LIT="FLOAT32_LIT"
        FLOAT64_LIT="FLOAT64_LIT"
        FLOAT_LIT="FLOAT_LIT"
        GENERALIZED_STR_LIT="GENERALIZED_STR_LIT"
        GENERALIZED_TRIPLESTR_LIT="GENERALIZED_TRIPLESTR_LIT"
        IDENT="IDENT"
        IND_EQ="IND_EQ"
        INDENT="INDENT"
        INT16_LIT="INT16_LIT"
        INT32_LIT="INT32_LIT"
        INT64_LIT="INT64_LIT"
        INT8_LIT="INT8_LIT"
        INT_LIT="INT_LIT"
        KEYW="KEYW"
        NIL="NIL"
        OP0A="OP0A"
        OP0B="OP0B"
        OP0C="OP0C"
        OP10A="OP10A"
        OP10B="OP10B"
        OP1A="OP1A"
        OP1B="OP1B"
        OP1C="OP1C"
        OP1D="OP1D"
        OP2A="OP2A"
        OP2B="OP2B"
        OP2C="OP2C"
        OP3A="OP3A"
        OP3B="OP3B"
        OP4A="OP4A"
        OP5A="OP5A"
        OP5B="OP5B"
        OP5C="OP5C"
        OP5D="OP5D"
        OP5E="OP5E"
        OP5F="OP5F"
        OP5G="OP5G"
        OP5H="OP5H"
        OP5I="OP5I"
        OP5J="OP5J"
        OP5K="OP5K"
        OP5L="OP5L"
        OP5M="OP5M"
        OP6A="OP6A"
        OP7A="OP7A"
        OP8A="OP8A"
        OP8B="OP8B"
        OP9A="OP9A"
        OP9B="OP9B"
        OP9C="OP9C"
        OP9D="OP9D"
        OP9E="OP9E"
        OP9F="OP9F"
        OP9G="OP9G"
        OPR="OPR"
        PARENTHESIS="PARENTHESIS"
        RSTR_LIT="RSTR_LIT"
        STR_LIT="STR_LIT"
        TRIPLESTR_LIT="TRIPLESTR_LIT"
        UINT16_LIT="UINT16_LIT"
        UINT32_LIT="UINT32_LIT"
        UINT64_LIT="UINT64_LIT"
        UINT8_LIT="UINT8_LIT"
        UINT_LIT="UINT_LIT"
    ]
}

//module = stmt ^* (';' / IND{=})
module ::= IND_EQ* stmts? ';'? IND_EQ*
stmts ::= stmt (sep stmt)*
private sep ::= ';' | IND_EQ+

//comma = ',' COMMENT?
//semicolon = ';' COMMENT?
//colon = ':' COMMENT?
//colcom = ':' COMMENT?

//operator =  OP0 | OP1 | OP2 | OP3 | OP4 | OP5 | OP6 | OP7 | OP8 | OP9
//         | 'or' | 'xor' | 'and'
//         | 'is' | 'isnot' | 'in' | 'notin' | 'of' | 'as' | 'from' |
//         | 'div' | 'mod' | 'shl' | 'shr' | 'not' | 'static' | '..'
operator ::= operator1
private operator1 ::= op0|op1|op2|op3|op4|op5|op6|op7|op8|op9|'from'|'static'|'..'
private op0 ::= OP0A|OP0B|OP0C
private op1 ::= OP1A|OP1B|OP1C|OP1D
private op2 ::= OP2A|/*OP2B|*/OP2C // NOTE: OP2B has been commented-out here due to ambiguities in the grammar.
private op3 ::= OP3A|OP3B
private op4 ::= OP4A
private op5 ::= OP5A|OP5B|OP5C|OP5D|OP5E|OP5F|OP5G|OP5H|OP5I|OP5J|OP5K|OP5L|OP5M
private op6 ::= OP6A
private op7 ::= OP7A
private op8 ::= OP8A|OP8B
private op9 ::= OP9A|OP9B|OP9C|OP9D|OP9E|OP9F|OP9G
private op10 ::= OP10A|OP10B

//prefixOperator = operator

//optInd = COMMENT? IND?
meta optInd ::= <<RULE>> | (INDENT <<RULE>>? (IND_EQ <<RULE>>?)* termInd)
private termInd ::= DEDENT | <<eof>>

//optPar = (IND{>} | IND{=})?

//simpleExpr = arrowExpr (OP0 optInd arrowExpr)* pragma?
//arrowExpr = assignExpr (OP1 optInd assignExpr)*
//assignExpr = orExpr (OP2 optInd orExpr)*
//orExpr = andExpr (OP3 optInd andExpr)*
//andExpr = cmpExpr (OP4 optInd cmpExpr)*
//cmpExpr = sliceExpr (OP5 optInd sliceExpr)*
//sliceExpr = ampExpr (OP6 optInd ampExpr)*
//ampExpr = plusExpr (OP7 optInd plusExpr)*
//plusExpr = mulExpr (OP8 optInd mulExpr)*
//mulExpr = dollarExpr (OP9 optInd dollarExpr)*
//dollarExpr = primary (OP10 optInd primary)*
simpleExpr ::= primary (op <<optInd primary>>)* pragma?
private op ::= op0|op1|op2|op3|op4|op5|op6|op7|op8|op9|op10|path // NOTE: Added 'path' here.
private path ::= ('./'|'../')+ // NOTE: Used in import expressions.

//symbol = '`' (KEYW|IDENT|literal|(operator|'('|')'|'['|']'|'{'|'}'|'=')+)+ '`'|IDENT|KEYW
symbol ::= symbol1
private symbol1 ::= symbol2|IDENT|KEYW
private symbol2 ::= '`' symbol3+ '`'
private symbol3 ::= KEYW|IDENT|literal|(operator|'('|')'|'['|']'|'{'|'}'|'=')+

//exprColonEqExpr = expr (':'|'=' expr)?
exprColonEqExpr ::= expr (exprColonEqExpr1 expr)?
private exprColonEqExpr1 ::= ':'|'='

//exprList = expr ^+ comma
exprs ::= expr (',' expr)*

//exprColonEqExprList = exprColonEqExpr (comma exprColonEqExpr)* comma?
exprColonEqExprs ::= exprColonEqExpr (',' exprColonEqExpr)* ','?

//dotExpr = expr '.' optInd (symbol | '[:' exprList ']')

//explicitGenericInstantiation = '[:' exprList ']' ( '(' exprColonEqExpr ')' )?

//qualifiedIdent = symbol ('.' optInd symbol)?
qualifiedIdent ::= symbol ('.' <<optInd symbol>>)?

//setOrTableConstr = '{' ((exprColonEqExpr comma)* | ':' ) '}'
setOrTableConstr ::= '{' setOrTableConstr1 '}'
private setOrTableConstr1 ::= ((exprColonEqExpr ',')+ | ':')?  // NOTE: Changed '*' to '+' + '?'

//castExpr = 'cast' '[' optInd typeDesc optPar ']' '(' optInd expr optPar ')'
castExpr ::= 'cast' '[' <<optInd typeDesc>> <<optInd (']')>> '(' <<optInd expr>> <<optInd (')')>>

//parKeyw = 'discard' | 'include' | 'if' | 'while' | 'case' | 'try'
//        | 'finally' | 'except' | 'for' | 'block' | 'const' | 'let'
//        | 'when' | 'var' | 'mixin'
//parKeyw ::= parKeyw1
//private parKeyw1 ::= 'discard' | 'include' | 'if' | 'while' | 'case' | 'try'
//          | 'finally' | 'except' | 'for' | 'block' | 'const' | 'let'
//          | 'when' | 'var' | 'mixin'

//par = '(' optInd
//          ( &parKeyw complexOrSimpleStmt ^+ ';'
//          | ';' complexOrSimpleStmt ^+ ';'
//          | pragmaStmt
//          | simpleExpr ( ('=' expr (';' complexOrSimpleStmt ^+ ';' )? )
//                       | (':' expr (',' exprColonEqExpr     ^+ ',' )? ) ) )
//          optPar ')'
par ::= '(' par1 ')'
private par1 ::= par2 | par3 | pragmaStmt | par4
private par2 ::= /*&parKeyw*/ stmt (';' stmt)*
private par3 ::= ';' stmt (';' stmt)*
private par4 ::= simpleExpr (par5 | par6)
private par5 ::= '=' expr (';' stmt (';' stmt)*)?
private par6 ::= ':' expr (',' exprColonEqExpr (',' exprColonEqExpr)*)?

//literal = | INT_LIT | INT8_LIT | INT16_LIT | INT32_LIT | INT64_LIT
//          | UINT_LIT | UINT8_LIT | UINT16_LIT | UINT32_LIT | UINT64_LIT
//          | FLOAT_LIT | FLOAT32_LIT | FLOAT64_LIT
//          | STR_LIT | RSTR_LIT | TRIPLESTR_LIT
//          | CHAR_LIT
//          | NIL
literal ::= literal1
private literal1 ::= INT_LIT | INT8_LIT | INT16_LIT | INT32_LIT | INT64_LIT
                   | UINT_LIT | UINT8_LIT | UINT16_LIT | UINT32_LIT | UINT64_LIT
                   | FLOAT_LIT | FLOAT32_LIT | FLOAT64_LIT
                   | STR_LIT | RSTR_LIT | TRIPLESTR_LIT
                   | CHAR_LIT
                   | BOOL_LIT // NOTE: BOOL_LIT was added here.
                   | NIL

//generalizedLit = GENERALIZED_STR_LIT | GENERALIZED_TRIPLESTR_LIT
generalizedLit ::= generalizedLit1
private generalizedLit1 ::= GENERALIZED_STR_LIT | GENERALIZED_TRIPLESTR_LIT

//identOrLiteral = generalizedLit | symbol | literal
//               | par | arrayConstr | setOrTableConstr
//               | castExpr
identOrLiteral ::= identOrLiteral1
private identOrLiteral1 ::= generalizedLit | symbol | literal
                          | par | arrayConstr | setOrTableConstr
                          | castExpr

//tupleConstr = '(' optInd (exprColonEqExpr comma?)* optPar ')'

//arrayConstr = '[' optInd (exprColonEqExpr comma?)* optPar ']'
arrayConstr ::= '[' (<<optInd (exprColonEqExpr ','?)>>)* <<optInd (']')>>

//primarySuffix = '(' (exprColonEqExpr comma?)* ')'
//      | '.' optInd symbol generalizedLit?
//      | '[' optInd exprColonEqExprList optPar ']'
//      | '{' optInd exprColonEqExprList optPar '}'
//      | &( '`'|IDENT|literal|'cast'|'addr'|'type') expr # command syntax
primarySuffix ::= primarySuffix0
private primarySuffix0 ::= primarySuffix1
                         | primarySuffix2
                         | primarySuffix3
                         | primarySuffix4
                         | primarySuffix5
private primarySuffix1 ::= '(' (exprColonEqExpr ','?)* ')'
private primarySuffix2 ::= '.' <<optInd (symbol generalizedLit?)>>
private primarySuffix3 ::= '[' (<<optInd exprColonEqExprs>>)? <<optInd (']')>>
private primarySuffix4 ::= '{' <<optInd exprColonEqExprs>> <<optInd ('}')>>
private primarySuffix5 ::= &('`'|IDENT|literal|'cast'|'addr'|'type') exprs // command syntax

//condExpr = expr colcom expr optInd
//        ('elif' expr colcom expr optInd)*
//         'else' colcom expr
condExpr ::= expr ':' <<optInd expr>> condExpr1* condExpr2? // NOTE: Added '?' to condExpr2
private condExpr1 ::= 'elif' expr ':' <<optInd expr>>
private condExpr2 ::= 'else' ':' expr

//ifExpr = 'if' condExpr
ifExpr ::= 'if' condExpr

//whenExpr = 'when' condExpr
whenExpr ::= 'when' condExpr

//pragma = '{.' optInd (exprColonEqExpr comma?)* optPar ('.}' | '}')
pragma ::= '{.' <<optInd ((exprColonEqExpr ','?)*)>> <<optInd pragma1>>
private pragma1 ::= '.}' | '}'

//identVis = symbol OPR?  # postfix position
//identVis ::= symbol oprCombo?  // postfix position

//identVisDot = symbol '.' optInd symbol OPR?
identVisDot ::= symbol ('.' <<optInd symbol>>)? oprCombo? // NOTE: '.' is now optional.
private oprCombo ::= /*OPR|*/op0|op1|op2|OP5A|OP5B|OP5C|OP5D|OP5E|OP5F|op6|op7|op8|OP9A|OP9B|OP9G|op10
// NOTE: OPR above has been commented-out to remove ambiguity.

//identWithPragma = identVis pragma?
identWithPragma ::= symbol oprCombo? pragma?

//identWithPragmaDot = identVisDot pragma?
identWithPragmaDot ::= identVisDot pragma?

//declColonEquals = identWithPragma (comma identWithPragma)* comma?
//                  (':' optInd typeDesc)? ('=' optInd expr)?
declColonEquals ::= identWithPragma (',' identWithPragma)* ','?
                    (':' <<optInd typeDesc>>)? ('=' <<optInd expr>>)?

//identColonEquals = IDENT (comma IDENT)* comma?
//     (':' optInd typeDesc)? ('=' optInd expr)?)
identColonEquals ::= IDENT (',' IDENT)* ','?
     (':' <<optInd typeDesc>>)? ('=' <<optInd expr>>)?

//inlTupleDecl = 'tuple'
//    '[' optInd  (identColonEquals (comma/semicolon)?)*  optPar ']'

//extTupleDecl = 'tuple'
//    COMMENT? (IND{>} identColonEquals (IND{=} identColonEquals)*)?

//tupleClass = 'tuple'

//paramList = '(' declColonEquals ^* (comma/semicolon) ')'
params ::= '(' (declColonEquals ((','|';') declColonEquals)*)? ')'

//paramListArrow = paramList? ('->' optInd typeDesc)?
paramsArrow ::= params? ('->' <<optInd typeDesc>>)?

//paramListColon = paramList? (':' optInd typeDesc)?
paramsColon ::= params? (':' <<optInd typeDesc>>)?

//doBlock = 'do' paramListArrow pragma? colcom stmt
doBlock ::= 'do' paramsArrow pragma? ':' <<optInd stmts>>

//procExpr = 'proc' paramListColon pragma? ('=' COMMENT? stmt)?

//distinct = 'distinct' optInd typeDesc

//forStmt = 'for' (identWithPragma ^+ comma) 'in' expr colcom stmt
forStmt ::= 'for' (identWithPragma (',' identWithPragma)*) 'in' expr ':' <<optInd stmts>>

//forExpr = forStmt

//expr = (blockExpr
//      | ifExpr
//      | whenExpr
//      | caseStmt
//      | forExpr
//      | tryExpr)
//      / simpleExpr
expr ::= expr1 | simpleExpr
private expr1 ::= blockStmt
                | ifExpr
                | whenExpr
                | caseStmt
                | forStmt
                | tryExpr

//typeKeyw = 'var' | 'out' | 'ref' | 'ptr' | 'shared' | 'tuple'
//         | 'proc' | 'iterator' | 'distinct' | 'object' | 'enum'
typeKeyw ::= typeKeyw1
private typeKeyw1 ::= 'var' | 'out' | 'ref' | 'ptr' | 'shared' | 'tuple'
                    /*| 'proc'*/ | 'iterator' | 'distinct' | 'object' | 'enum'
// NOTE: The 'proc' keyword has been commented-out and handled explicitly in the 'primary' rule below.

//primary = typeKeyw optInd typeDesc
//        /  prefixOperator* identOrLiteral primarySuffix*
//        / 'bind' primary
primary ::= primary1 | primary2 | primary3
private primary1 ::= typeKeyw <<optInd typeDesc>>
private primary2 ::= operator? identOrLiteral primarySuffix*
private primary3 ::= 'bind' primary

//typeDesc = simpleExpr ('not' expr)?
typeDesc ::= typeDesc0
private typeDesc0 ::= typeDesc1 | typeDesc2
private typeDesc1 ::= 'proc' paramsColon // NOTE: This is a new rule handle 'proc' typed parameters.
private typeDesc2 ::= simpleExpr ('not' expr)?

//typeDefAux = simpleExpr ('not' expr)?
//           | 'concept' typeClass
typeDefAux ::= typeDefAux0
private typeDefAux0 ::= simpleExpr ('not' expr)? | typeDefAux1
private typeDefAux1 ::= 'concept' typeClass

//postExprBlocks = ':' stmt? ( IND{=} doBlock
//                           | IND{=} 'of' exprList ':' stmt
//                           | IND{=} 'elif' expr ':' stmt
//                           | IND{=} 'except' exprList ':' stmt
//                           | IND{=} 'else' ':' stmt )*
postExprBlocks ::= ':' <<optInd stmts>>? postExprBlocks0*
private postExprBlocks0 ::= postExprBlocks1
                          | postExprBlocks2
                          | postExprBlocks3
                          | postExprBlocks4
                          | postExprBlocks5
private postExprBlocks1 ::= IND_EQ doBlock
private postExprBlocks2 ::= IND_EQ 'of' exprs ':' <<optInd stmts>>
private postExprBlocks3 ::= IND_EQ 'elif' expr ':' <<optInd stmts>>
private postExprBlocks4 ::= IND_EQ 'except' exprs ':' <<optInd stmts>>
private postExprBlocks5 ::= IND_EQ 'else' ':' <<optInd stmts>>

//exprStmt = simpleExpr
//         (( '=' optInd expr colonBody? )
//         / ( expr ^+ comma
//             postExprBlocks
//           ))?
exprStmt ::= simpleExpr (exprStmt1 | exprStmt2)?
private exprStmt1 ::= '=' <<optInd expr>> colonBody?
private exprStmt2 ::= expr (',' expr)* postExprBlocks

//importStmt = 'import' optInd expr
//              ((comma expr)*
//              / 'except' optInd (expr ^+ comma))
importStmt ::= 'import' <<optInd expr>> ((',' expr)+ | importStmt1)
private importStmt1 ::= 'except' <<optInd (expr (',' expr)*)>>

//exportStmt = 'export' optInd expr
//              ((comma expr)*
//              / 'except' optInd (expr ^+ comma))
exportStmt ::= 'export' <<optInd expr>> ((',' expr)+ | exportStmt1)
private exportStmt1 ::= 'except' <<optInd (expr (',' expr)*)>>

//includeStmt = 'include' optInd expr ^+ comma
includeStmt ::= 'include' <<optInd expr>> (',' expr)*

//fromStmt = 'from' expr 'import' optInd expr (comma expr)*
fromStmt ::= 'from' expr 'import' <<optInd expr>> (',' expr)*

//returnStmt = 'return' optInd expr?
returnStmt ::= 'return' <<optInd expr>>?

//raiseStmt = 'raise' optInd expr?
raiseStmt ::= 'raise' <<optInd expr>>?

//yieldStmt = 'yield' optInd expr?
yieldStmt ::= 'yield' <<optInd expr>>?

//discardStmt = 'discard' optInd expr?
discardStmt ::= 'discard' <<optInd expr>>?

//breakStmt = 'break' optInd expr?
breakStmt ::= 'break' <<optInd expr>>?

//continueStmt = 'break' optInd expr?
continueStmt ::= 'break' <<optInd expr>>?

//condStmt = expr colcom stmt COMMENT?
//           (IND{=} 'elif' expr colcom stmt)*
//           (IND{=} 'else' colcom stmt)?
condStmt ::= expr ':' <<optInd stmts>> condStmt1* condStmt2?
private condStmt1 ::= IND_EQ 'elif' expr ':' <<optInd stmts>>
private condStmt2 ::= IND_EQ 'else' ':' <<optInd stmts>>

//ifStmt = 'if' condStmt
ifStmt ::= 'if' condStmt

//whenStmt = 'when' condStmt
whenStmt ::= 'when' condStmt

//whileStmt = 'while' expr colcom stmt
whileStmt ::= 'while' expr ':' <<optInd stmts>>

//ofBranch = 'of' exprList colcom stmt
ofBranch ::= 'of' exprs ':' <<optInd stmts>>

//ofBranches = ofBranch (IND{=} ofBranch)*
//                      (IND{=} 'elif' expr colcom stmt)*
//                      (IND{=} 'else' colcom stmt)?
ofBranches ::= ofBranch (IND_EQ ofBranch)* ofBranches1* ofBranches2?
private ofBranches1 ::= IND_EQ 'elif' expr ':' <<optInd stmts>>
private ofBranches2 ::= IND_EQ 'else' ':' <<optInd stmts>>

//caseStmt = 'case' expr ':'? COMMENT?
//            (IND{>} ofBranches DED
//            | IND{=} ofBranches)
caseStmt ::= 'case' expr ':'? caseStmt0
private caseStmt0 ::= caseStmt1 | caseStmt2
private caseStmt1 ::= INDENT ofBranches DEDENT
private caseStmt2 ::= IND_EQ ofBranches

//tryStmt = 'try' colcom stmt &(IND{=}? 'except'|'finally')
//           (IND{=}? 'except' exprList colcom stmt)*
//           (IND{=}? 'finally' colcom stmt)?
tryStmt ::= 'try' ':'stmt &(IND_EQ? 'except'|'finally') tryStmt1* tryStmt2?
private tryStmt1 ::= IND_EQ? 'except' exprs ':' stmt
private tryStmt2 ::= IND_EQ? 'finally' ':' stmt

//tryExpr = 'try' colcom stmt &(optInd 'except'|'finally')
//           (optInd 'except' exprList colcom stmt)*
//           (optInd 'finally' colcom stmt)?
tryExpr ::= 'try' ':' <<optInd stmts>> &(<<optInd ('except'|'finally')>>) tryExpr1* tryExpr2?
private tryExpr1 ::= <<optInd ('except' exprs ':' <<optInd stmts>>)>>
private tryExpr2 ::= <<optInd ('finally' ':' <<optInd stmts>>)>>

//exceptBlock = 'except' colcom stmt

//blockStmt = 'block' symbol? colcom stmt
//blockExpr = 'block' symbol? colcom stmt
blockStmt ::= 'block' symbol? ':' <<optInd stmts>>

//staticStmt = 'static' colcom stmt
staticStmt ::= 'static' ':' <<optInd stmts>>

//deferStmt = 'defer' colcom stmt
deferStmt ::= 'defer' ':' <<optInd stmts>>

//asmStmt = 'asm' pragma? (STR_LIT|RSTR_LIT|TRIPLESTR_LIT)
asmStmt ::= 'asm' pragma? asmStmt1
private asmStmt1 ::= STR_LIT|RSTR_LIT|TRIPLESTR_LIT

//genericParam = symbol (comma symbol)* (colon expr)? ('=' optInd expr)?
genericParam ::= symbol (',' symbol)* (':' expr)? ('=' <<optInd expr>>)?

//genericParamList = '[' optInd
//  genericParam ^* (comma/semicolon) optPar ']'
genericParams ::= '[' <<optInd ((genericParam ((','|';') genericParam)*)?)>> <<optInd (']')>>

//pattern = '{' stmt '}'
pattern ::= '{' <<optInd stmts>> '}'

//indAndComment = (IND{>} COMMENT)? | COMMENT?

//routine = optInd identVis pattern? genericParamList?
//  paramListColon pragma? ('=' COMMENT? stmt)? indAndComment
routine ::= pattern? genericParams? paramsColon pragma? routine1?
private routine1 ::= '=' (stmt | (INDENT stmts termInd))

//commentStmt = COMMENT

//section(RULE) = COMMENT? RULE / (IND{>} (RULE / COMMENT)^+IND{=} DED)
//meta section ::= <<RULE>> | (INDENT <<RULE>>? (IND_EQ <<RULE>>?)* termInd)

//objectWhen = 'when' expr colcom objectPart COMMENT?
//            ('elif' expr colcom objectPart COMMENT?)*
//            ('else' colcom objectPart COMMENT?)?

//objectBranch = 'of' exprList colcom objectPart

//objectBranches = objectBranch (IND{=} objectBranch)*
//                      (IND{=} 'elif' expr colcom objectPart)*
//                      (IND{=} 'else' colcom objectPart)?

//objectCase = 'case' identWithPragma ':' typeDesc ':'? COMMENT?
//            (IND{>} objectBranches DED
//            | IND{=} objectBranches)

//objectPart = IND{>} objectPart^+IND{=} DED
//           / objectWhen / objectCase / 'nil' / 'discard' / declColonEquals

//object = 'object' pragma? ('of' typeDesc)? COMMENT? objectPart

//typeClassParam = ('var' | 'out')? symbol
typeClassParam ::= ('var'|'out')? symbol

//typeClass = typeClassParam ^* ',' (pragma)? ('of' typeDesc ^* ',')? &IND{>} stmt
typeClass ::= (typeClassParam (',' typeClassParam)*)? pragma? ('of' (typeDesc (',' typeDesc)*)?)?

//typeDef = identWithPragmaDot genericParamList? '=' optInd typeDefAux
//            indAndComment? / identVisDot genericParamList? pragma '=' optInd typeDefAux
//            indAndComment?
typeDef ::= typeDef1 | typeDef2
typeDef1 ::= identWithPragmaDot genericParams? '=' <<optInd typeDefAux>>
typeDef2 ::= identVisDot genericParams? pragma '=' <<optInd typeDefAux>>

//varTuple = '(' optInd identWithPragma ^+ comma optPar ')' '=' optInd expr
varTuple ::= '(' <<optInd (identWithPragma (',' identWithPragma)*)>> <<optInd (')')>> '=' <<optInd expr>>

//colonBody = colcom stmt postExprBlocks?
colonBody ::= ':' <<optInd stmts>> postExprBlocks?

//variable = (varTuple / identColonEquals) colonBody? indAndComment
variable ::= (varTuple | identColonEquals) colonBody?

//constant = (varTuple / identWithPragma) (colon typeDesc)? '=' optInd expr indAndComment
constant ::= (varTuple | identWithPragma) (':' typeDesc)? '=' <<optInd expr>>

//bindStmt = 'bind' optInd qualifiedIdent ^+ comma
bindStmt ::= 'bind' <<optInd (qualifiedIdent (',' qualifiedIdent)*)>>

//mixinStmt = 'mixin' optInd qualifiedIdent ^+ comma
mixinStmt ::= 'mixin' <<optInd (qualifiedIdent (',' qualifiedIdent)*)>>

//pragmaStmt = pragma (':' COMMENT? stmt)?
pragmaStmt ::= pragma (':' <<optInd stmts>>)?

//simpleStmt = ((returnStmt | raiseStmt | yieldStmt | discardStmt | breakStmt
//           | continueStmt | pragmaStmt | importStmt | exportStmt | fromStmt
//           | includeStmt | commentStmt) / exprStmt) COMMENT?
simpleStmt ::= simpleStmt1 | exprStmt
private simpleStmt1 ::= returnStmt | raiseStmt | yieldStmt | discardStmt | breakStmt
                      | continueStmt | pragmaStmt | importStmt | exportStmt | fromStmt
                      | includeStmt

//complexOrSimpleStmt = (ifStmt | whenStmt | whileStmt
//                    | tryStmt | forStmt
//                    | blockStmt | staticStmt | deferStmt | asmStmt
//                    | 'proc' routine
//                    | 'func' routine
//                    | 'method' routine
//                    | 'iterator' routine
//                    | 'macro' routine
//                    | 'template' routine
//                    | 'converter' routine
//                    | 'type' section(typeDef)
//                    | 'const' section(constant)
//                    | ('let' | 'var' | 'using') section(variable)
//                    | bindStmt | mixinStmt)
//                    / simpleStmt

//stmt = (IND{>} complexOrSimpleStmt^+(IND{=} / ';') DED) / simpleStmt ^+ ';'

stmt ::= complexStmt | simpleStmt
private complexStmt ::= ifStmt
                      | whenStmt
                      | whileStmt
                      | tryStmt
                      | forStmt
                      | blockStmt
                      | staticStmt
                      | deferStmt
                      | asmStmt
                      | procStmt
                      | funcStmt
                      | methodStmt
                      | iteratorStmt
                      | macroStmt
                      | templateStmt
                      | converterStmt
                      | typeStmt
                      | constStmt
                      | varStmt
                      | bindStmt
                      | mixinStmt

procStmt ::= 'proc' <<optInd namedRoutine>>
funcStmt ::= 'func' <<optInd namedRoutine>>
methodStmt ::= 'method' <<optInd namedRoutine>>
iteratorStmt ::= 'iterator' <<optInd namedRoutine>>
macroStmt ::= 'macro' <<optInd namedRoutine>>
templateStmt ::= 'template' <<optInd namedRoutine>>
converterStmt ::= 'converter' <<optInd namedRoutine>>
typeStmt ::= 'type' <<optInd typeDef>>
constStmt ::= 'const' <<optInd constant>>
varStmt ::= ('let'|'var'|'using') <<optInd variable>>

namedRoutine ::= symbol oprCombo? routine {methods=[nameIdentifier="symbol"]}